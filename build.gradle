//Gradle file library ev3dev-lang-java
//Author: Juan Antonio Breña Moral

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'java'
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id "jacoco"
    id 'checkstyle'
    id 'maven-publish'
    id 'com.github.ev3dev-lang-java.gradle-plugin' version '2.0.0'
    id "net.ossindex.audit" version "0.4.5-beta"
    id "com.github.johnrengelman.shadow" version "4.0.3"
}

version = '2.6.1-SNAPSHOT'

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

lombok {
    version = '1.18.2'
    sha256 = ""
}

dependencies {
    checkstyle 'com.puppycrawl.tools:checkstyle:8.2'

    annotationProcessor("org.projectlombok:lombok:1.16.20")
    compileOnly("org.projectlombok:lombok:1.16.20")
    testCompileOnly("org.projectlombok:lombok:1.16.20")

    api("org.slf4j:slf4j-api:1.7.25")
    api("com.github.ev3dev-lang-java:lejos-commons:0.7.3")
    api("net.java.dev.jna:jna:4.5.2")

    testImplementation("ch.qos.logback:logback-classic:1.2.3")

    testImplementation("commons-io:commons-io:2.5")

    testImplementation("junit:junit:4.12")
    testImplementation("org.hamcrest:hamcrest-all:1.3")
}

compileJava.options.encoding = 'UTF-8'

//Compile
compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

test.testLogging {
    exceptionFormat "full"
}

//Coverage
jacoco {
    toolVersion = "0.8.2"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                //minimum = 0.75 // implement more tests
                minimum = 0.3
            }
        }
    }
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
}

test.finalizedBy jacocoTestReport
test.finalizedBy jacocoTestCoverageVerification

apply from: './gradle/config.gradle'
apply from: './gradle/docker.gradle'

//Jar
jar {
    baseName = "${rootProject.name}"
    manifest {
        attributes("Implementation-Title": "EV3Dev-lang-java",
                   "Implementation-Version": version,
                   "Implementation-Vendor": "Juan Antonio Breña Moral",
                   "Main-Class": brick.pref.mainClass,
                   "Class-Path": brick.getClassPath(true) )
    }
}

task fatJar {
    doLast {

    }
}
fatJar.dependsOn shadowJar



/* global settings */
configure(allprojects) { project ->

    compileJava.options*.compilerArgs = [
            "-Xlint:unchecked", "-Xlint:rawtypes",
    ]

    compileTestJava.options*.compilerArgs = [
            "-Xlint:deprecation", "-Xlint:unchecked", "-Xlint:rawtypes",
    ]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

//TODO Review failing classes in the next release
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.github.ev3dev-lang-java'
            artifactId = 'ev3dev-lang-java'
            version = "${version}"

            from components.java
            artifact (sourcesJar) {
                classifier = 'sources'
            }
            //artifact (javadocJar) {
            //    classifier = 'javadoc'
            //}
        }
    }
}
